@top MindmapDiagram {
  newline+ | 
  DiagramName Line*
}

@skip { space | newlineEmpty }

lineText {
  LineText1 |
  LineText2 |
  LineText3 |
  LineText4 |
  LineText5
}

shapedText {
  Square |
  RoundedSquare |
  Circle |
  Bang |
  Cloud |
  Hexagon
}

Square {
  "[" lineText "]"
}

RoundedSquare {
  "(" lineText ")"
}

Circle {
  "((" lineText "))"
}

Bang {
  "))" lineText "(("
}

Cloud {
  ")" lineText "("
}

Hexagon {
  "{{" lineText "}}"
}

IconLine {
  "::" Icon "(" lineText ")"
}

ClassLine {
  ":::" lineText
}

Line {
  newline |
  newline indent (
    lineText |
    IconLine |
    ClassLine |
    shapedText |
    lineText shapedText
  )
}

DiagramName {
  kw<"mindmap">
}

Icon {
  kw<"icon">
}

kw<term> { @specialize<word, term> }

@context trackIndent from "./tokens.js"

@external tokens indentation from "./tokens" { indent }

@external tokens lineTextType from "./tokens" { 
  LineText1,
  LineText2,
  LineText3,
  LineText4,
  LineText5
}

@tokens {
  space { ($[ \t\f] | "\\" $[\n\r])+ }

  word { @asciiLetter+ }

  "(" ")"

  "((" "))"

  "[" "]"

  "{{" "}}"

  "::"
}

@external tokens newlines from "./tokens" { newline, newlineEmpty }

@external propSource mindmapHighlighting from "./highlight"

@detectDelim