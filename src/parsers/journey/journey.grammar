@top JourneyDiagram {
  document
}

@skip { spaces }

document {
  DiagramName newlines* (
    () |
    subDocument newlines* |
    subDocument (newlines+ subDocument)+ newlines*
  )
}

subDocument {
  LineComment |
  Keyword Text |
  Task
}

Task {
  TaskName ":" Score (":" Actor ("," Actor)*)?
}

Text {
  text1
}

TaskName {
  text2
}

Score {
  text2
}

Actor {
  text3
}

DiagramName { kw<"journey"> }

kw<term> { @specialize<identifier, term> }

@external tokens keywordTokens from "./tokens" { Keyword }
@external tokens textTokens1 from "./tokens" { text1 }
@external tokens textTokens2 from "./tokens" { text2 }
@external tokens textTokens3 from "./tokens" { text3 }

@tokens {
  spaces { @whitespace+ }
  newlines { $[\n]+ }
  LineComment { "%%" ![\n]* }
  identifier { @asciiLetter+ }

  @precedence { newlines, spaces }
}

@external propSource journeyHighlighting from "./highlight"
